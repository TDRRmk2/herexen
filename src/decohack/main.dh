/*****************************************************************************
 * DECOHack Patch File
 * Main
 ****************************************************************************/

#include <doom19>
#include <friendly>

strings
{
	STR_GOTSTIM "Crystal Vial"
	STR_GOTMEDINEED "Quartz Flask"
	STR_GOTMEDIKIT "Quartz Flask"
	STR_GOTARMOR "Falcon Shield"
	STR_GOTMEGA "Chainmail Armor"
	STR_GOTSUPER "Mystic Urn"

	STR_GOTCLIP "Energy Orb"
	STR_GOTCLIPBOX "Large Energy Orb"
	STR_GOTCELL "Flame Orb"
	STR_GOTCELLBOX "Inferno Orb"

	STR_GOTCHAINSAW "Gauntlets of the Necromancer"
	STR_GOTSHOTGUN "Jade Wand"
	STR_GOTSHOTGUN2 "Ruby Wand"
	STR_GOTCHAINGUN "Blaster"
	STR_GOTLAUNCHER "Hammer of Retribution"
	STR_GOTPLASMA "Phoenix Rod"
	STR_GOTBFG9000 "Fire Spell"

	STR_SPR_STIM "PTN1"
	STR_SPR_MEDI "PTN2"
	STR_SPR_ARM1 "FALC"
	STR_SPR_ARM2 "CMAL"
	STR_SPR_BON2 "ARM3"
	STR_SPR_SOUL "SPHL"
	STR_SPR_PVIS "TRCH"

	STR_SPR_CLIP "AMB1"
	STR_SPR_AMMO "AMB2"
	STR_SPR_CELL "AMP1"
	STR_SPR_CELP "AMP2"

	STR_SPR_CSAW "WGNT"
	STR_SPR_SHOT "WJAD"
	STR_SPR_SGN2 "WRUB"
	STR_SPR_MGUN "WBLS"
	STR_SPR_LAUN "WFHM"
	STR_SPR_PLAS "WPHX"
	STR_SPR_BFUG "WMCF"

	STR_SPR_PUNG "FPCH"
	STR_SPR_SAWG "GAUN"
	STR_SPR_PISG "MWND"
	STR_SPR_SHTG "JADE"
	STR_SPR_SHT2 "RUBY"
	STR_SPR_CHGG "BLSR"
	STR_SPR_MISG "FHMR"
	STR_SPR_PLSG "PHNX"
	STR_SPR_BFGG "CONF"
	STR_SPR_MISL "FHFX"
	STR_SPR_PLSS "FX09"
	STR_SPR_BFS1 "NONE"
	STR_SPR_BFE1 "SPIR"

	STR_SPR_POSS "MUMM"
	STR_SPR_TROO "FDMN"
	STR_SPR_BAL1 "FX15"
	STR_SPR_SARG "ETTN"
	STR_SPR_HEAD "LICH"
	STR_SPR_SKEL "KNIG"
	STR_SPR_BAL2 "FX07"
	STR_SPR_SKUL "IMPX"
	STR_SPR_VILE "BISH"
	STR_SPR_PLSE "BISG"
	STR_SPR_MANF "RAXE"
	STR_SPR_CYBR "MNTR"
	STR_SPR_FATT "ICEY"
	STR_SPR_FATB "ICPR"
	STR_SPR_PAIN "WRTH"
	STR_SPR_BOSS "CENT"
	STR_SPR_BOS2 "NULL"
	STR_SPR_BAL7 "CTFX"
	STR_SPR_BSPI "SNKE"
}

thing WolfensteinSS free states
thing WolfensteinSS : thing ChaingunGuy {ednum 84}

thing CommanderKeen free states
//thing SpiderMastermind free states

#define QLOWER_OFFS Offset(1, 72)
#define QRAISE_OFFS Offset(1, 62)

//state protect 52 to 53
state protect S_PLASMAFLASH1 to S_PLASMAFLASH2

thing GreenArmor free states
thing BlueArmor free states
thing ArmorBonus free states
thing SoulSphere free states
thing Infrared free states

thing Clip free states
thing ClipBox free states
thing Cell free states
thing CellPack free states

weapon Fist free states
weapon Chainsaw free states
weapon Pistol free states
weapon Shotgun free states
weapon SuperShotgun free states
weapon Chaingun free states
weapon RocketLauncher free states
thing Rocket free states
weapon PlasmaRifle free states
thing PlasmaBall free states
weapon BFG9000 free states
thing BFGBall free states
thing BFGExtra free states
thing BFG9000 free states

thing Zombieman free states
thing ShotgunGuy free states
thing DoomImp free states
thing DoomImpBall free states
thing Demon free states
thing Cacodemon free states
thing CacodemonBall free states
thing Revenant free states
thing RevenantTracer free states
thing PainElemental free states
thing LostSoul free states
thing Archvile free states
thing ArchvileFire free states
thing Fatso free states
thing FatShot free states
thing HellKnight free states
thing BaronOfHell free states
thing BaronBall free states
thing ArachnotronPlasma free states
thing Arachnotron free states
thing Cyberdemon free states

thing GreenArmor "Falcon Shield"
{
	clear states

	states
	{
	Spawn:
		FALC A 6
		FALC A 7 Bright
		Loop
	}
}

thing BlueArmor "Chainmail Armor"
{
	clear states

	states
	{
	Spawn:
		CMAL A 6
		CMAL A 7 Bright
		Loop
	}
}

thing ArmorBonus
{
	clear states

	states
	{
	Spawn:
		ARM3 A 6
		ARM3 A 7 Bright
		Loop
	}
}

thing SoulSphere "Mystic Urn"
{
	clear states

	states
	{
	Spawn:
		SPHL A 6
		SPHL A 7 Bright
		Loop
	}
}

thing Infrared "Torch"
{
	clear states

	states
	{
	Spawn:
		TRCH AB 3 Bright
		Loop
	}
}

thing Clip "Energy Orb (small)"
{
	clear states

	states
	{
	Spawn:
		AMB1 ABC 4 Bright
		Loop
	}
}

thing ClipBox "Energy Orb (large)"
{
	clear states

	states
	{
	Spawn:
		AMB2 ABC 4 Bright
		Loop
	}
}

thing Cell "Energy Orb (small)"
{
	clear states

	states
	{
	Spawn:
		AMP1 ABC 4 Bright
		Loop
	}
}

thing CellPack "Energy Orb (large)"
{
	clear states

	states
	{
	Spawn:
		AMP2 ABC 4 Bright
		Loop
	}
}

weapon Fist
{
	clear states
	states
	{
	Select:
		FPCH A 0 QRAISE_OFFS
		FPCH A 1 A_Raise
		Wait
	Deselect:
		FPCH A 0 QLOWER_OFFS
		FPCH A 1 A_Lower
		Wait
	Ready:
		FPCH A 1 A_WeaponReady
		Loop
	Fire:
		FPCH B 5 Offset (5, 40)
		FPCH C 4
		FPCH D 4 { A_Punch A_Punch A_Punch }
		FPCH C 4
		FPCH B 5 A_ReFire
		Goto Ready
	}
}

weapon Chainsaw "Necromancer Gauntlets"
{
	clear states
	states
	{
	Select:
		GAUN A 0 QRAISE_OFFS
		GAUN A 1 A_Raise
		Wait
	Deselect:
		GAUN A 0 QLOWER_OFFS
		GAUN A 1 A_Lower
		Wait
	Ready:
		GAUN ABC 2 A_WeaponReady
		Loop
	Fire:
		GAUN D 2 Offset (1, 32) A_Saw
		GAUN EF 2 A_Saw
		Goto Ready
	}
}

weapon Pistol "Gold Wand"
{
	clear states
	states
	{
	Ready:
		MWND A 1 A_WeaponReady
		Loop
	Deselect:
		MWND A 0 QLOWER_OFFS
		MWND A 1 A_Lower
		Wait
	Select:
		MWND A 0 QRAISE_OFFS
		MWND A 1 A_Raise
		Wait
	Fire:
		MWND B 0 A_FirePistol
		MWND B 6 Bright Offset(1, 48) A_FirePistol
		MWND A 6 Offset(1, 40)
		MWND A 3 Offset(1, 32)
		Goto Ready
	Flash:
		MWND B 1 Bright A_Light2
		Goto S_LIGHTDONE
	}
}

weapon Shotgun "Jade Wand"
{
	clear states
	states
	{
	Ready:
		JADE A 1 A_WeaponReady
		Loop
	Deselect:
		JADE A 0 QLOWER_OFFS
		JADE A 1 A_Lower
		Wait
	Select:
		JADE A 0 QRAISE_OFFS
		JADE A 1 A_Raise
		Wait
	Fire:
		JADE B 6 Bright Offset(1, 52) A_FireShotgun
		JADE A 5 Offset(1, 48)
		JADE A 6 Offset(1, 46)
		JADE A 6 Offset(1, 40)
		JADE A 6 Offset(1, 32)
		JADE A 0 A_ReFire
		Goto Ready
	}
}

weapon SuperShotgun "Ruby Wand"
{
	clear states
	states
	{
	Ready:
		RUBY A 1 Bright A_WeaponReady
		Loop
	Deselect:
		RUBY A 0 QLOWER_OFFS
		RUBY A 1 Bright A_Lower
		Wait
	Select:
		RUBY A 0 QRAISE_OFFS
		RUBY A 1 Bright A_Raise
		Wait
	Fire:
		RUBY B 8 Bright Offset(1, 60) A_FireShotgun2
		RUBY A 7 Bright Offset(1, 48)
		RUBY A 8 Offset(1, 46) A_CheckReload
		RUBY A 8 Offset(1, 40)
		RUBY A 8 Offset(1, 36)
		RUBY A 8 Offset(1, 32)
		RUBY A 0 A_ReFire
		Goto Ready
	}
}

weapon Chaingun "Blaster"
{
	clear states
	states
	{
	Ready:
		BLSR A 0 A_ReFire
		BLSR A 1 Bright A_WeaponReady
		Loop
	Deselect:
		BLSR A 0 QLOWER_OFFS
		BLSR A 1 Bright A_Lower
		Wait
	Select:
		BLSR A 0 QRAISE_OFFS
		BLSR A 1 Bright A_Raise
		Wait
	Fire:
		BLSR D 0 Offset(1, 32) A_FirePistol
		BLSR D 2 Bright A_FirePistol
		BLSR CB 2 Bright
		BLSR A 0 A_ReFire
		Goto Ready
	}
}

weapon RocketLauncher "Hammer"
{
	clear states
	states
	{
	Ready:
		FHMR A 1 A_WeaponReady
		Loop
	Deselect:
		FHMR A 0 QLOWER_OFFS
		FHMR A 1 A_Lower
		Wait
	Select:
		FHMR A 0 QRAISE_OFFS
		FHMR A 1 A_Raise
		Wait
	Fire:
		FHMR B 6 Offset(5, 32)
		FHMR C 3 Offset(5, 32)
		FHMR D 3 Offset(5, 32)
		FHMR E 2 Offset(5, 32)
		FHMR E 10 Offset(5, 150) A_FireMissile
		FHMR A 3 Offset(0, 60)
		FHMR A 2 Offset(0, 40)
		FHMR A 1 A_ReFire
		Goto Ready
	}
}

thing Rocket "Hammer projectile"
{
	clear states
	states
	{
	Spawn:
		FHFX ACEG 4 Bright
		Loop
	Death:
		FHFX I 6 Bright
		FHFX K 6 Bright A_Explode
		FHFX M 6 Bright
		FHFX OQ 6 Bright
		Stop
	}
}

state unprotect S_PLASMAFLASH1 to S_PLASMAFLASH2

state S_PLASMAFLASH1
{
	NONE A 1 A_CheckReload
	Stop
}

state S_PLASMAFLASH2
{
	NONE A 1 A_CheckReload
	Stop
}

state protect S_PLASMAFLASH1 to S_PLASMAFLASH2

weapon PlasmaRifle "Phoenix Rod"
{
	clear state Ready
	clear state Deselect
	clear state Select
	clear state Fire
	states
	{
	Ready:
		PHNX A 1 A_WeaponReady
		Loop
	Deselect:
		PHNX A 0 QLOWER_OFFS
		PHNX A 1 A_Lower
		Wait
	Select:
		PHNX A 0 QRAISE_OFFS
		PHNX A 1 A_Raise
		Wait
	Fire:
		PHNX C 1 Bright Offset(1, 32) A_FirePlasma
		PHNX D 1 Bright A_FirePlasma
		PHNX B 7 Bright A_ReFire
		Goto Ready
	}
}

thing PlasmaBall "Phoenix Rod Flame"
{
	Damage 3
	Speed 30
	Radius 2
	Height 4

	clear states
	states
	{
	Spawn:
		FX09 ABAB 4 Bright
		Stop
	Death:
		FX09 A 2 Bright
		Stop
	}
}

weapon BFG9000 "Fire Spell"
{
	clear states
	states
	{
	Select:
		CONF A 0 QRAISE_OFFS
		CONF A 1 A_Raise
		Wait
	Deselect:
		CONF A 0 QLOWER_OFFS
		CONF A 1 A_Lower
		Wait
	Ready:
		CONF AABBCC 2 A_WeaponReady
		Loop
	Fire:
		CONF D 3 Offset(1, 32)
		CONF EF 3
		CONF G 5 Bright
		CONF H 3 Bright { A_FireBFG A_BFGsound }
		CONF FED 3
		CONF A 2 A_ReFire
		Goto Ready
	}
}

thing BFGBall "Fire Spell Helper"
{
	Damage 0
	Speed 0
	Radius 0
	Height 0

	clear states
	states
	{
	Spawn:
		NONE A 0
	Death:
		NONE A 2 A_BFGSpray
		Stop
	}
}

thing BFGExtra
{
	clear states
	states
	{
	Spawn:
		FIRE A 4 Bright
		FIRE C 4 Bright A_Explode
		FIRE EG 4 Bright
		Stop
	}
}

thing BFG9000
{
	clear states
	states
	{
	Spawn:
		WMCF ABCB 4 Bright
		Loop
	}
}

thing Zombieman "Golem"
{
	Health 30
	Painchance 128
	Speed 6

	clear states
	states
	{
	Spawn:
		MUMM A 10 A_Look
		Loop
	See:
		MUMM AABBCCDD 2 A_Chase
		Loop
	Melee:
		MUMM E 6 A_FaceTarget
		MUMM F 5 A_SargAttack
		MUMM G 5
		Goto See
	Pain:
		MUMM H 8 A_Pain
		Goto See
	Death:
		MUMM I 5
		MUMM J 5 A_Scream
		MUMM KL 5
		MUMM M 5 A_Fall
		MUMM NO 5
		MUMM P -1
		Stop
	}
}

thing ShotgunGuy : thing Zombieman "Nitrogolem"
{
	PainChance 64
	Health 50
	ednum 9

	states
	{
	Missile:
		MUMM R 4 A_FaceTarget
		MUMM Q 4 Bright A_FaceTarget
		MUMM R 4 A_FaceTarget
		MUMM Q 4 Bright A_FaceTarget
		MUMM R 4 A_FaceTarget
		MUMM Q 4 Bright A_TroopAttack
		Goto See
	}
}

thing DoomImp "Afrit"
{
	Speed 13
	+FLOAT
	+NOGRAVITY

	clear states
	states
	{
	Spawn:
		FDMN A 5 Bright A_Look
		Loop
	See:
		FDMN ABC 5 Bright A_Chase
		Loop
	Pain:
		FDMN D 6 Bright A_Pain
		Goto See
	Melee:
	Missile:
		FDMN K 3 Bright A_FaceTarget
		FDMN KKK 5 Bright A_TroopAttack
		Goto See
	Death:
		FDMN D 4 Bright
		FDMN L 4 Bright A_Scream
		FDMN L 4 Bright A_Fall
		FDMN M 5
		FDMN N 5
		FDMN O 5
		FDMN P -1
		Stop
	}
}

thing DoomImpBall "Nitrogolem Projectile"
{
	clear states
	states
	{
	Spawn:
		FX15 ABCB 5 Bright
		Loop
	Death:
		FX15 DEFG 5 Bright
		Stop
	}
}

thing Demon "Ettin"
{
	Speed 16

	clear states
	states
	{
	Spawn:
		ETTN A 10 A_Look
		Loop

	See:
		ETTN ABCD 5 A_Chase
		Loop

	Pain:
		ETTN H 7 A_Pain
		Goto See

	Melee:
		ETTN EF 4 A_FaceTarget
		ETTN G 4 A_SargAttack
		Goto See

	Death:
		ETTN IJ 4
		ETTN K 4 A_Scream
		ETTN L 4 A_Fall
		ETTN M 4
		ETTN NOP 4
		ETTN Q -1
		Stop
	}
}

thing Spectre : thing Demon "Cloaked Ettin"
{
	ednum 58
	+SHADOW
}

thing Cacodemon "Iron Lich"
{
	Speed 5
	clear states

	states
	{
	Spawn:
		LICH A 10 A_Look
		Loop
	See:
		LICH A 3 A_Chase
		Loop
	Missile:
		LICH A 5 A_FaceTarget
		LICH B 20 A_HeadAttack
		Goto See
	Pain:
		LICH A 8 A_Pain
		Goto See
	Death:
		LICH C 7
		LICH D 7 A_Scream
		LICH EF 7
		LICH G 7 A_Fall
		LICH H 7
		LICH I -1 //A_BossDeath
		Stop
	}
}

thing CacodemonBall "Iron Lich Whirlwind"
{
	+FLOAT
	Damage 0
	clear states

	states
	{
	Spawn:
		FX07 DEFG 3
	Test:
		FX07 ABC 3
		Goto Test
	See:
		FX07 AAA 0 A_Chase
		Goto DeathEnd
	Melee:
		FX07 A 0 A_VileAttack
		Goto DeathEnd
	Death:
		FX07 G 1 A_Look
	DeathEnd:
		FX07 G 3
		FX07 FED 4
		Stop
	}
}

thing PainElemental "Reiver"
{
	clear states

	states
	{
	Spawn:
		WRTH AB 10 A_Look
		Loop
	See:
		WRTH ABCD 4 A_Chase
		Loop
	Pain:
		WRTH A 2
		WRTH H 6 A_Pain
		Goto See
	Melee:
	Missile:
		WRTH E 6 A_FaceTarget
		WRTH F 6 A_FaceTarget
		WRTH G 6 { A_SkelFist A_PainAttack }
		Goto See
	Death:
		WRTH I 4
		WRTH J 4 A_Scream
		WRTH KL 4
		WRTH M 4 A_PainDie
		WRTH NO 4
		WRTH PQ 5
		WRTH R -1
		Stop
	}
}

thing LostSoul "Gargoyle"
{
	Speed 10
	clear states

	states
	{
	Spawn:
		IMPX ABCB 10 A_Look
		Loop
	See:
		IMPX AABBCCBB 3 A_Chase
		Loop
	Melee:
		IMPX DE 6 A_FaceTarget
		IMPX F 6 A_SargAttack
		Goto See
	Missile:
		IMPX A 10 A_FaceTarget
	Fly:
		IMPX B 6 A_SkullAttack
		IMPX CBAB 6
		Goto Fly
	Pain:
		IMPX G 3
		IMPX G 3 A_Pain
		Goto See
	Death:
		IMPX G 4 A_Scream
		IMPX H 7 A_Fall
		IMPX IJKL 7
		Stop
	}
}

thing Revenant "Knight"
{
	clear states

	states
	{
	Spawn:
		KNIG A 10 A_Look
		Loop
	See:
		KNIG AABBCCDD 2 A_Chase
		Loop
	Melee:
	Missile:
		KNIG E 10 A_FaceTarget
		KNIG F 8 A_FaceTarget
		KNIG G 8 A_SkelMissile
		KNIG E 10 A_FaceTarget
		KNIG F 8 A_FaceTarget
		KNIG G 8 A_SkelMissile
		Goto See
	Pain:
		KNIG H 6 A_Pain
		Goto See
	Death:
		KNIG I 6
		KNIG J 6 A_Scream
		KNIG K 6
		KNIG L 6 A_Fall
		KNIG MN 6
		KNIG O -1
		Stop
	}
}

thing RevenantTracer "Knight Axe"
{
	Damage 5
	clear states

	states
	{
	Spawn:
		RAXE A 2 Bright A_Tracer
		Loop
	Death:
		RAXE BCD 6 Bright
		Stop
	}
}

thing RevenantTracerSmoke : thing Blood "Knight Axe Blood" {}

thing Archvile "Bishop"
{
	Speed -40

	clear states
	states
	{
	Spawn:
		BISH A 10 A_Look
		Loop
	See:
		BISH A 35
		TFOG AC 5 Bright
		BISH AAAA 0 A_VileChase
	Missile:
		TFOG C 5 Bright A_VileTarget
		TFOG A 5 Bright A_VileTarget
		BISH D 4 Bright A_BruisAttack
		BISH E 4 Bright A_FaceTarget
		BISH F 8 Bright	A_TroopAttack
		Goto See
	Heal:
		BISH DEF 10 Bright
		Goto See
	Death:
		BISH H 6 Bright A_Scream
		BISH I 5 Bright A_Fall
		BISH JKLMN 4
		BISH P -1
		Stop
	}
}

thing ArchvileFire "Bishop Clone"
{
	-SOLID
	+SHOOTABLE
	-NOBLOCKMAP
	Health 1
	Radius 8
	Speed -50

	clear states
	states
	{
	Spawn:
		NONE A 1
		NONE A 1 A_Look
		Wait
	See:
		BISG AAAAA 0 A_Chase
		TFOG CA 5 Bright A_FaceTarget
		BISG D 4 Bright A_FaceTarget
		BISG E 4 Bright A_FaceTarget
		BISG F 8 Bright	A_TroopAttack
		Goto S_TFOG
	Death:
		NONE A 1
		Stop
	}
}

thing Fatso "Wendigo"
{
	Painchance 144
	Speed 14

	clear states
	states
	{
	Spawn:
		ICEY A 10 A_Look
		Loop
	See:
		ICEY ABCD 4 A_Chase
		Loop
	Pain:
		ICEY A 4 A_Pain
		Goto See
	Missile:
		ICEY EF 3 A_FaceTarget
		ICEY G 8 Bright { A_FatAttack1 A_FatAttack2 A_FatAttack3 }
		ICEY F 4
		Goto See
	Death:
		ICEY H 5 A_Scream
		ICEY I 5 A_Fall
		ICEY JKLM 5
		ICEY N -1 A_BossDeath
		Stop
	}
}

thing FatShot "Wendigo Shot"
{
	Radius 8
	Height 10
	Speed 14
	Damage 4

	clear states
	states
	{
	Spawn:
		ICPR ABC 3 Bright
		Loop

	Death:
		ICPR DEFG 4 Bright
		ICPR H 3 Bright
		Stop
	}
}

thing BaronOfHell "Slaughtaur"
{
	+TRANSLATION
	+UNUSED1
	PainChance 96
	Mass 100000
	Speed 10

	clear states
	states
	{
	Spawn:
		CENT A 10 A_Look
		Loop
	See:
		CENT ABCD 4 A_Chase
		Loop
	Pain:
		CENT G 12 A_Pain
		CENT EEE 15 { A_Chase A_BspiAttack }
		Goto See
	Melee:
		CENT H 5 A_FaceTarget
		CENT I 4 A_FaceTarget
		CENT J 7 A_SkelFist
		Goto See
	Missile:
		CENT E 10 A_FaceTarget
		CENT F 8 Bright A_BruisAttack
		CENT E 10 A_FaceTarget
		CENT F 8 Bright A_BruisAttack
		Goto See
	Death:
		CENT K 4
		CENT L 4 A_Scream
		CENT MN 4
		CENT O 4 A_Fall
		CENT PQRS 4
		CENT T -1
		Stop
	}
}

thing HellKnight : thing BaronOfHell "Slaughtaur"
{
	-UNUSED1
	PainChance 135
	Health 500
	ednum 69
	Speed 13

	clear state Missile
}

thing BaronBall "Slaughtaur Shot"
{
	Speed 20
	Damage 4

	clear states
	states
	{
	Spawn:
		CTFX A 1 Bright
		Loop
	Death:
		CTFX B 4 Bright
		CTFX C 3 Bright
		CTFX D 4 Bright
		CTFX E 3 Bright
		CTFX F 2 Bright
		Stop
	}
}

thing ArachnotronPlasma
{
	+NOBLOOD
	+SHOOTABLE
	-NOGRAVITY
	-NOBLOCKMAP
	-MISSILE
	-SOLID
	Mass 100000
	Radius 26
	Height 64
	Speed 2

	clear states
	states
	{
	Spawn:
	Death:
		NULL A 0
		NULL A 15 A_Chase
		Stop
	}
}

thing Arachnotron "Ophidian"
{
	Radius 24

	clear states
	states
	{
	Spawn:
		SNKE A 10 A_Look
		Loop
	See:
		SNKE ABCD 4 A_Chase
		Loop
	Missile:
		SNKE FF 5 A_FaceTarget
		SNKE FFF 4 A_TroopAttack
		SNKE FFF 5 A_FaceTarget
		SNKE F 4 A_BruisAttack
		Goto See
	Pain:
		SNKE E 3
		SNKE E 3 A_Pain
		Goto See
	Death:
		SNKE G 5
		SNKE H 5 A_Scream
		SNKE IJKL 5
		SNKE M 5 A_Fall
		SNKE NO 5
		SNKE P -1 A_BossDeath
		Stop
	}
}

thing Cyberdemon "Maulotaur"
{
	Painchance 40
	Speed 16

	AttackSound "OOF"

	clear states
	states
	{
	Spawn:
		MNTR A 10 A_Look
		Loop
	See:
		MNTR ABCD 5 A_Chase
		Loop
	Missile:
		MNTR V 10 A_FaceTarget
		MNTR Y 4 A_FaceTarget
		MNTR Z 5 A_CyberAttack
		MNTR V 5 A_FaceTarget
		MNTR Y 4 A_FaceTarget
		MNTR Z 5 A_CyberAttack
		MNTR V 5 A_FaceTarget
		MNTR Y 4 A_FaceTarget
		MNTR Z 9 A_CyberAttack
		Goto See
	Pain:
		MNTR E 6 A_Pain
		MNTR U 5 A_SkullAttack
		MNTR UUU 10 A_Explode
		Goto See
	Death:
		MNTR F 6
		MNTR G 5
		MNTR H 6 A_Scream
		MNTR I 5
		MNTR J 6
		MNTR K 5
		MNTR L 6
		MNTR M 5 A_Fall
		MNTR N 6
		MNTR O 5
		MNTR P 6
		MNTR Q 5
		MNTR R 6
		MNTR S 5
		MNTR T -1 A_BossDeath
		Stop
	}
}

thing DeadLostSoul
{
	clear states
	states
	{
	Spawn:
		NULL A 1
		Stop
	}
}

thing DeadCacodemon
{
	clear states
	states
	{
	Spawn:
		LICH I 1
		Loop
	}
}

thing DeadDemon
{
	clear states
	states
	{
	Spawn:
		ETTN Q 1
		Loop
	}
}

thing DeadDoomImp
{
	clear states
	states
	{
	Spawn:
		FDMN P 1
		Loop
	}
}

thing DeadZombieman
{
	clear states
	states
	{
	Spawn:
		MUMM P 1
		Loop
	}
}

thing DeadShotgunGuy : thing DeadZombieman
{
	ednum 19
}

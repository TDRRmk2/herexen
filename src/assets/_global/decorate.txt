// GLDEFS having no way to clear lights sure is cool
Actor Herexen_BFGBall : BFGBall replaces BFGBall
{
	Damage 0
	Speed 0
	Radius 0
	Height 0

	states
	{
	Spawn:
		NONE A 0
	Death:
		NONE A 2 A_BFGSpray
		Stop
	}
}

Actor Herexen_Zombieman : Zombieman replaces Zombieman
{
	Health 50
	Painchance 128
	Speed 6

	AttackSound ""

	states
	{
	Spawn:
		MUMM A 10 A_Look
		Loop
	See:
		MUMM AABBCCDD 2 A_Chase
		Loop
	Melee:
		MUMM E 6 A_FaceTarget
		MUMM F 5 A_SargAttack
		MUMM G 5
		Goto See
    Missile:
        Stop
	Pain:
		MUMM H 8 A_Pain
		Goto See
	Death:
		MUMM I 5
		MUMM J 5 A_Scream
		MUMM KL 5
		MUMM M 5 A_Fall
		MUMM NO 5
		MUMM P -1
		Stop
    XDeath:
        Stop
	Raise:
		MUMM ONMLKJI 5
		Goto See
	}
}

Actor Herexen_ShotgunGuy : Herexen_Zombieman replaces ShotgunGuy
{
    Obituary "$OB_SHOTGUY"
	PainChance 64
	Health 60

	states
	{
	Missile:
		MUMM R 4 A_FaceTarget
		MUMM Q 4 Bright A_FaceTarget
		MUMM R 4 A_FaceTarget
		MUMM Q 4 Bright A_FaceTarget
		MUMM R 4 A_FaceTarget
		MUMM Q 0 A_TroopAttack
		MUMM Q 4 Bright A_TroopAttack
		Goto See
	}
}

Actor Herexen_ChaingunGuy : ChaingunGuy replaces ChaingunGuy
{
	states
	{
	Spawn:
		MAGE A 10 A_Look
		Loop
	See:
		MAGE AABBCCDD 3 A_Chase
		Loop
	Missile:
		MAGE E 10 A_FaceTarget
	Refiring:
		MAGE FE 4 Bright A_CPosAttack
		MAGE F 1 A_CPosRefire
		Goto Refiring
	Pain:
		MAGE G 3
		MAGE G 3 A_Pain
		Goto See
	Death:
		MAGE H 5
		MAGE I 5 A_Scream
		MAGE J 5 A_Fall
		MAGE KLM 5
		MAGE N -1
		Stop
	XDeath:
		MAGE O 5
		MAGE P 5 A_XScream
		MAGE Q 5 A_Fall
		MAGE RSTUVW 5
		MAGE X -1
		Stop
	Raise:
		MAGE N 5
		MAGE MLKJIH 5
		Goto See
	}
}

Actor Herexen_CacodemonBall : CacodemonBall replaces CacodemonBall
{
	+FLOAT
	Damage 0
	Speed 14

	states
	{
	Spawn:
		FX07 DEFG 3
	InFlight:
		FX07 ABC 3
		Loop
	See:
		FX07 AAA 0 A_Chase
		Goto DeathEnd
	Melee:
		FX07 A 0 A_VileAttack
		Goto DeathEnd
	Death:
		FX07 G 1 A_Look
	DeathEnd:
		FX07 G 3
		FX07 FED 4
		Stop
	}
}

Actor Herexen_BaronBall : BaronBall replaces BaronBall
{
	Speed 20
	Damage 4

	states
	{
	Spawn:
		CTFX A 1 Bright
		Loop
	Death:
		CTFX B 4 Bright
		CTFX C 3 Bright
		CTFX D 4 Bright
		CTFX E 3 Bright
		CTFX F 2 Bright
		Stop
	}
}

Actor Herexen_ArachnotronPlasma : ArachnotronPlasma replaces ArachnotronPlasma
{
	+NOBLOOD
	+SHOOTABLE
	-NOGRAVITY
	-NOBLOCKMAP
	-MISSILE
	-SOLID
	Mass 100000
	Radius 26
	Height 64
	Speed 2

	SeeSound ""
	ActiveSound ""
	AttackSound ""
	DeathSound ""

	states
	{
	Spawn:
	Death:
		NULL A 0
		NULL A 14 A_Chase
		Stop
	}
}

Actor Herexen_Archvile : Archvile replaces Archvile
{
	Speed -40

	states
	{
	Spawn:
		BISH A 10 A_Look
		Loop
	See:
		BISH A 35
	TeleportAway:
		TFOG AC 5 Bright
		NONE A 1
		BISH AAAA 0 A_VileChase
		NONE A 1
	Missile:
		TFOG C 6 Bright A_VileTarget
		TFOG A 6 Bright A_VileTarget
		BISH D 4 Bright A_BruisAttack
		BISH E 4 Bright A_FaceTarget
		BISH F 8 Bright	A_TroopAttack
		Goto See
	Pain:
		BISH C 6
		BISH C 6 A_Pain
		Goto TeleportAway
	Death:
		BISH H 6 Bright A_Scream
		BISH I 5 Bright A_Fall
		BISH JKLMN 4
		BISH P -1
		Stop
	}
}

Actor Herexen_LostSoul : LostSoul replaces LostSoul
{
	Speed 10

	states
	{
	Spawn:
		IMPX ABCB 10 A_Look
		Loop
	See:
		IMPX AABBCCBB 3 A_Chase
		Loop
	Melee:
		IMPX DE 6 A_FaceTarget
		IMPX F 6 A_SargAttack
		Goto See
	Missile:
		IMPX A 10 A_FaceTarget
	Fly:
		IMPX B 6 A_SkullAttack
		IMPX CBAB 6
		Goto Fly
	Pain:
		IMPX G 3
		IMPX G 3 A_Pain
		Goto See
	Death:
		IMPX G 4 A_Scream
		IMPX H 7 A_Fall
		IMPX IJKL 7
		Stop
	}
}

Actor Herexen_Soulsphere : Soulsphere replaces Soulsphere
{
	states
	{
	Spawn:
		SPHL A 6
		SPHL A 7 Bright
		Loop
	}
}

Actor Herexen_GreenArmor : GreenArmor replaces GreenArmor
{
	states
	{
	Spawn:
		FALC A 6
		FALC A 7 Bright
		Loop
	}
}

Actor Herexen_BlueArmor : BlueArmor replaces BlueArmor
{
	states
	{
	Spawn:
		CMAL A 6
		CMAL A 7 Bright
		Loop
	}
}

// oddly has no lights in GZDoom for some reason?
Actor Herexen_Infrared : Infrared replaces Infrared
{
	states
	{
	Spawn:
		TRCH AB 3 Bright
		Loop
	}
}

Actor HerexenPlayer : DoomPlayer replaces DoomPlayer {}
